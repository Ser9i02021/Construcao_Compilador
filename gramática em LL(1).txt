PROGRAM -> STATEMENT | FUNCLIST | &
FUNCLIST -> FUNCDEF FUNCLIST'
FUNCLIST' -> FUNCDEF FUNCLIST' | &
FUNCDEF -> def ident(PARAMLIST){STATELIST}
PARAMLIST -> PARAMETRO PARAMLIST' | &
PARAMETRO -> int ident | float ident | string ident
PARAMLIST' -> , PARAMETRO PARAMLIST' | &
STATEMENT -> VARDECL; | ATRIBSTAT; | PRINTSTAT; | READSTAT; | RETURNSTAT; | IFSTAT; | FORSTAT; | {STATELIST} | break; | ;
VARDECL -> int ident CONSTANTE | float ident CONSTANTE | string ident CONSTANTE
CONSTANTE -> [const_inteiro] CONSTANTE | &
ATRIBSTAT -> LVALUE = ATRIBSTAT'
ATRIBSTAT' -> EXPRESSION | ALLOCEXPRESSION
FUNCCALL -> (PARAMLISTCALL)
PARAMLISTCALL -> ident PARAMLISTCALL' | &
PARAMLISTCALL' -> , ident PARAMLISTCALL' | &
PRINTSTAT -> print EXPRESSION
READSTAT -> read LVALUE
RETURNSTAT -> return
IFSTAT -> if( EXPRESSION ) STATEMENT IFSTAT'
IFSTAT' -> else STATEMENT | &
FORSTAT -> for(ATRIBSTAT; EXPRESSION; ATRIBSTAT) STATEMENT
STATELIST -> STATEMENT STATELIST | &
ALLOCEXPRESSION -> new TIPO [NUMEXPRESSION] ALLOCEXPRESSION'
TIPO -> int | float | string
ALLOCEXPRESSION' -> [NUMEXPRESSION] | &
EXPRESSION -> NUMEXPRESSION COMPARACAO
COMPARACAO -> < NUMEXPRESSION | > NUMEXPRESSION | <= NUMEXPRESSION | >= NUMEXPRESSION | == NUMEXPRESSION | != NUMEXPRESSION | &
NUMEXPRESSION -> TERM NUMEXPRESSION'
NUMEXPRESSION' -> + TERM NUMEXPRESSION' | - TERM NUMEXPRESSION' | &
TERM -> UNARYEXPR TERM'
TERM' -> * UNARYEXPR TERM' | / UNARYEXPR TERM' | % UNARYEXPR TERM' | &
UNARYEXPR -> OPERADORES FACTOR
OPERADORES -> + | - | &
FACTOR -> const_inteiro | const_float | const_string | null | (NUMEXPRESSION) | ident CHAMADA
CHAMADA -> FUNCCALL | LVALUE'
LVALUE -> ident LVALUE'
LVALUE' -> [NUMEXPRESSION] LVALUE' | &